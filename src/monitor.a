;; rsbohn 2019-05-20
;; piebox monitor.a
	!cpu 65816

	RA = $00
	RW = $04
	RS = $08
	RV = $2C	;; video RAM (TEXT) [vlow][vhigh][vbank][column]

	KCHAN = $1E

	EV = $0000
	VIA0	= $0800
	VIDEO	= $0840
	TIB	= $6F00
	STACK	= $7FFF
	NVECT	= $FFE8
	EVECT	= $FFFA
	VRAM	= $8000		;; in databank $01
	CGFILL	= $017000	;; fill chargen

	!source "src/macros.a"
	
	* = $F000
COLD:
	CLC
	XCE
	CLI

	+long
	LDA MSTACK
	TCS

	JSR ICONSOLE
	;JSR BOOTMSG
	JSR VSTART
	+long
	+const $8000
	STA RV
	+const $0001
	STA RV+2
	JSL CGFILL

	JSR MSTART
	;; run the Smalltalk VM
	JMP BCBOOT

;;;;;;;;;;;;;;;;;;;;
MSTACK:
	!word STACK

FVOUT:
	!byte $10
FVIN:
	!byte $08
CONSOLE:
	!word $0810
CONCTL:
	!word $071E
ICONSOLE:
	;; initialize serial console
	+long
	PHY
	PHA
	LDA CONSOLE
	STA KCHAN
	+short
	LDY CH+03
	LDA CONCTL
	STA (KCHAN),Y
	LDA CONCTL+1
	DEY
	STA (KCHAN),Y
	+long
	PLA
	PLY
	RTS

BOOTMSG:
	JSR PRINTME
	!raw "Pluton/0 65816 Computer", $0A, $0D
	!raw "Edition 2019.06", $0A, $0D, $0A, $00
	RTS

PRINTME:	;; print the embedded string
	+long
	PLA
	INC
	STA RA
-	+short
	LDA (RA)
	BEQ +	;; done printing
	JSR CHOUT
	INC RA	;; short mode 16 bit incr
	BNE -
	INC RA+1
	BRA -
+	+long
	LDA RA
	PHA
	RTS

EIAIN:
	+short
	PHY
	LDY CH+01
-	LDA FVIN
	AND (KCHAN),Y
	BEQ -
	LDA (KCHAN)
	PLY
	RTS
	
EIAOUT:
	+short
	PHY
	PHA
	LDY CH+01
-	LDA FVOUT
	AND (KCHAN),Y
	BEQ -
	PLA
	DEY
	STA (KCHAN),Y
	PLY
	RTS
DBHEX:
	+long
	PHA
	XBA
	JSR HEXO
	+long
	PLA
	JSR HEXO
	+short
	LDA #$20
	JMP CHOUT
HEXO:
	+short
	STA RA
	LSR
	LSR
	LSR
	LSR
	JSR .HEX
	LDA RA
.HEX:	AND #$0F
	CLC
	ADC #$30
	CMP #$3A
	BCC +
	ADC #$06
+	JSR CHOUT
	RTS
DBNL:
	+short
	LDA NEWLINE
	JSR CHOUT
	RTS
CRLF:
	+short
	LDA NEWLINE
	JSR CHOUT
	LDA NEWLINE+1
	JSR CHOUT
	RTS

	.IRA	= $01
	.IFR	= $0D

CHIN:
	+short
-	+byteAt .IFR, VIA0
	AND #$02
	BEQ -
	SEI
	+byteAt .IFR, VIA0
	EOR #$02
	+byteAtPut .IFR, VIA0
	CLI
	+byteAt .IRA, VIA0
	RTS

CHOUT:
	+short
	PHY
	LDY RV+3
	STA [RV],Y
	INC RV+3
	PLY
	RTS
;;;;;;;;;;;;;;;;;;;;;;;;;;;
VSTART:
	+short
	LDX #$00
-	STX VIDEO
	LDA VIDEOP,X
	STA VIDEO+1
	INX
	CPX #$10
	BNE -
	RTS

VIDEOP:
	!raw $7F, $50, $60, $08
	!raw $21, $06, $18, $1C
	!raw $00, $08, $20, $09
	!raw $00, $00, $00, $00


;;;;;;;;;;;;;;;;;;;;;;;;;;;
	!source "src/bytecodes.a"
	!source "src/primitive.a"
	!source "src/memory.a"
;;;;;;;;;;;;;;;;;;;;;;;;;;;

	* = $FFC0
CH:
	!byte 0,1,2,3,4,5,6,7,8,9

MPROMPT:
	!byte '?'
MSPACE:
	!byte $20
NEWLINE:
	!byte $0A, $0D


	* = $FFF0
ERESET:
	JMP COLD
NVIRQ:
HANG:
	BRA HANG
NVABORT:
NVNMI:
NVBRK:
ENMI:
EIRQ:
	JMP HANG
	
	* = NVECT
	!word NVABORT
	!word NVNMI
	!word NVBRK
	!word NVIRQ
	
	* = EVECT
	!word ENMI
	!word ERESET
	!word EIRQ
	
